{
package parse
}

DOCUMENT <- entries:ENTRY* EOF {
	return newTXs(entries)
}

ENTRY <- date:DATE state:HEADSTATE? _ payee:PAYEE NL postings:POSTING+ {
	return newTX(date, state, payee, postings)
}

HEADSTATE <- SP SIGIL SP
POSTSTATE <- sig:SIGIL SP {
  return sig, nil
}

SIGIL <- sig:("*" / "!") {
  return newState(sig)
}

POSTING <- SP _  st:POSTSTATE? act:ACCOUNT _ amt:AMOUNT NL {
  return newPost(st, act, amt)
}

ACCOUNT <- CHAR+ (":" CHAR+)* {
  return string(c.text), nil
}

AMOUNT <- "$" neg:"-"? units:DIGITS "." cents:CENTS {
  return newAmount(neg, units, cents)
}

DATE <- YEAR "-" MONTH "-" DAY {
	return time.Parse("2006-01-02", string(c.text))
}

PAYEE <- CHAR+ {
	return string(c.text), nil
}

CHAR <- [A-Za-z']
DIGITS <- DIGIT+ {
  return c.text, nil
}

CENTS <- DIGIT DIGIT {
  return c.text, nil
}

YEAR <- DIGIT DIGIT DIGIT DIGIT
MONTH <- DIGIT DIGIT
DAY <- DIGIT DIGIT

_ "ignored whitespace" <- [ \n\t\r]*
SP "significant whitespace" <- " "
NL "newline" <- "\n"

DIGIT <- [0-9]

EOF <- !.
